---
page_title: "zstack_networking_secgroup_rule Resource - terraform-provider-zstack"
subcategory: ""
description: |-
    This resource manages a security group rule in ZStack.
---

# zstack_networking_secgroup_rule (Resource)

This resource manages a security group rule in ZStack.

## Example Usage

```terraform
# Create a security group first (assuming you have a security group resource)
resource "zstack_networking_secgroup" "linuxbridge_test" {
  name         = "tf-secgroup-linuxbridge-for-rules"
  description  = "Test Security Group with LinuxBridge"
  vswitch_type = "LinuxBridge"
  ip_version   = 4
}

# Create an ingress rule
resource "zstack_networking_secgroup_rule" "ingress_http" {
  name                    = "allow-http-ingress"
  security_group_uuid     = zstack_networking_secgroup.linuxbridge_test.uuid
  direction               = "Ingress"
  action                  = "ACCEPT"
  protocol                = "TCP"
  priority                = 2
  ip_version              = 4
  ip_ranges               = "10.0.1.0/8,30.1.1.0/24"
  destination_port_ranges = "80"
  description             = "Allow HTTP traffic from anywhere"
  state                   = "Disabled"
}

# Create an egress rule with a specific port range
resource "zstack_networking_secgroup_rule" "egress_custom" {
  name                    = "allow-custom-egress"
  security_group_uuid     = zstack_networking_secgroup.linuxbridge_test.uuid
  direction               = "Egress"
  action                  = "ACCEPT"
  protocol                = "TCP"
  priority                = 1
  ip_version              = 4
  ip_ranges               = "192.168.0.0/16"
  destination_port_ranges = "8000-9000"
  description             = "Allow custom port range traffic to internal network"
  state                   = "Enabled"
}

# Create a rule for ICMP protocol
resource "zstack_networking_secgroup_rule" "icmp_rule" {
  depends_on          = [zstack_networking_secgroup_rule.ingress_http]
  name                = "allow-icmp"
  security_group_uuid = zstack_networking_secgroup.linuxbridge_test.uuid
  direction           = "Ingress"
  action              = "ACCEPT"
  protocol            = "ICMP"
  priority            = 1
  ip_version          = 4
  ip_ranges           = "20.0.0.0/0"
  description         = "Allow ICMP traffic (ping) from anywhere"
  state               = "Enabled"
}


# Output the created resources
output "ingress_http_rule_id" {
  value = zstack_networking_secgroup_rule.ingress_http
}

output "egress_custom_rule_id" {
  value = zstack_networking_secgroup_rule.egress_custom
}

output "icmp_rule_id" {
  value = zstack_networking_secgroup_rule.icmp_rule
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `action` (String) The action to take when the rule matches, either 'ACCEPT' or 'DROP'.
- `direction` (String) The direction of the rule, either 'Ingress' or 'Egress'.
- `ip_ranges` (String) The IP ranges for the rule, e.g., '10.0.0.0/24' or '10.0.0.1-10.0.0.10'. IP ranges. For Ingress rules, maps to `srcIpRange`. For Egress rules, maps to `dstIpRange`.
- `ip_version` (Number) The IP version, either '4' or '6'.
- `name` (String) A name for the security group rule.
- `priority` (Number) Priority of the rule. Must start from 1 and be consecutive (e.g., 1, 2, 3, 4). Skipping values (e.g., 1, 3, 10) is not allowed.
- `protocol` (String) The protocol, either 'TCP', 'UDP', or 'ICMP'.
- `security_group_uuid` (String) The UUID of the security group to which this rule belongs.
- `state` (String) The state of the rule, either 'Enabled' or 'Disabled'.

### Optional

- `description` (String) A description for the security group rule.
- `destination_port_ranges` (String) The destination port ranges, e.g., '80,443' or '8080-9090'.
- `remote_security_group_uuid` (String) The UUID of the remote security group to associate with this rule.

### Read-Only

- `uuid` (String) The UUID of the security group rule.


